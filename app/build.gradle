apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.sonarqube'

sonarqube {
  properties {
    property "sonar.projectKey", "appsoluut_android-multi-module-test"
    property "sonar.organization", "appsoluut"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

android {
    compileSdkVersion 31
    buildToolsVersion "32.0.0"

    signingConfigs {
        release {
            storeFile file("../cert/android_signkey_sanoma.keystore")
            storePassword "s4n0m4m0b1l"
            keyAlias "sanomamedia"
            keyPassword "s4n0m4m0b1l"
        }
    }
    
    defaultConfig {
        applicationId "com.appsoluut.android.myapplication"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode generateVersionCode()
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
                signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation project(":mylibrary")

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

private static Integer generateVersionCode() {
    // max value is 2100.000.000
    def gitCommit

    def androidVersionCode = System.getenv("GITHUB_RUN_NUMBER")
    if (androidVersionCode != null) {
        androidVersionCode = 200000 + Integer.valueOf(androidVersionCode)
        println("*** Version determined by Environment variable")
        println("*** Version Code: " + androidVersionCode)
        return androidVersionCode.toInteger()
    }

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        gitCommit = ['cmd', '/c', 'git rev-list HEAD --count'].execute().text.trim()
    } else {
        gitCommit = ['sh', '-c', 'git rev-list HEAD | wc -l | tr -d " "'].execute().text.trim()
    }

    androidVersionCode = Integer.valueOf(gitCommit + "0")
    println("*** Version determined by Git commit count")
    println("*** Version Code: " + androidVersionCode)
    return androidVersionCode
}
